apply plugin: "java"
apply plugin: "cobertura"
apply plugin: "maven-publish"

def getVersion = { ->
  def out = new ByteArrayOutputStream()
  exec {
    commandLine "git", "describe", "--tags"
    standardOutput = out
  }
  return out.toString().trim()
}

group = "io.jamal"
version = getVersion();

repositories {
  mavenCentral()
}

dependencies {
  compile     "org.commonjava.googlecode.markdown4j:markdown4j:2.2-cj-1.0"
  runtime     "xerces:xercesImpl:2.11.0"
  testCompile "junit:junit:4.+"
}

test {
  exclude "io/jamal/JamalTestCase.class"
}

buildscript {
   repositories {
       mavenCentral()
   }
   dependencies {
       classpath "net.saliman:gradle-cobertura-plugin:2.2.4"
   }
}

task cssZip(type: Zip) {
  from 'src/main/css'
}
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact cssZip {
              classifier "css"
            }
        }
    }
    repositories {
      mavenLocal()
    }
}
